-> ChapterZero
=== ChapterZero ===
VAR inAd = false
<b> 第零章：入职档案 </b>
-> Prologue
= Prologue
<b> 序幕：招聘启事与暗示 </b>
三个月前。
那是四月的第一场雨。而你却高兴不起来。
公寓里墙上的钟表指针指向11:23。电脑屏幕在黑暗中发出冷光。你已经失业三个月，银行余额在红色警戒线徘徊。当第47封拒信抵达邮箱时，一个你从未订阅的邮件列表弹出了新消息：
* 继续
<b> 标题：市立档案馆特殊岗位招聘·编号0427</b>
发件人：人力资源部（但邮箱后缀是@archieve-domain.local，一个不存在的域名）
邮件正文：
尊敬的候选人：
市立档案馆因业务扩展，现公开招聘档案管理员一名。
<b> 职位要求 </b>：
· 能够忍受长时间静默工作
· 对异常细节有敏锐察觉力
· 不追问超出权限的问题
· 能够接受非标准工作时间
· <b> 最重要的是：拥有分辨事实与虚构的能力 </b>
<b>我们不要求的：</b>
· 相关工作经验（新手有时更合适）
· 过高学历（知识可能成为障碍）
· 强烈的职业野心（这里没有晋升通道）
<b>特别说明：</b>
本职位采用<b>“双向选择制”</b>。如果您收到此邮件，说明您已被初步筛选。但最终是否录用，取决于您是否通过后续的“无声测试”。
请在明日上午9:47分，携带此邮件打印件，前往档案馆侧门（非正门）报到。迟到者自动放弃资格。
无需回复。
<b>邮件附件：</b>一张黑白建筑照片，档案馆的西南角，拍摄角度显示了一个普通游客不会注意到的青铜门。门把手上刻着细小的符号--后来你会发现，那即是闭眼符号的早期变体。
-> Choice0 -> Act1
= Act1
档案馆西南角小巷。
四月的冷雨刚刚停歇，路面反光如破碎镜面。青铜门隐藏在常春藤后，上面写着门牌号：影巷47号。
空气中弥漫着旧纸、霉菌和某种类似臭氧的混合气味。周围异常安静，连城市背景音都似乎被吸收。
{ inAd:
        -> hidden0 ->
}
时间来到9:47分整。面前的青铜门完全无声地滑开，内部是深不见底的黑暗。
-> Act2_0
= Act2_0
当你适应了黑暗，黑暗之中的场景映入你的眼帘。门后是一条没有灯的走廊，长约30米。外部光线在进门后立即消失，你仿佛跨过了一个无形的边界。门内的空气温度恒定在18°C，无风，但深处传来了纸张的轻微翻动声。墙壁并非水泥或石材建成，而是...有些类似厚皮革的质地。脚下的地面有微弱弹性，行走无声。
一路向前，你注意到了一些东西。左侧墙壁显现出了壁画。每幅壁画尺寸30x40cm，黑木框。一共有13幅，上面的所有人物都闭着眼睛。而右侧则是7个尺寸相同的的空画框，但空画框之间又有着细微的不同。
*   上前仔细观察这些壁画和空画框，尝试分析它们的细节
    -> Act2_1
*   联想到之前的信，还是不做这些为好
    -> Act2_5
= Act2_1
*   观察肖像壁画
    序号为奇数的壁画，人物都闭着眼，但嘴角微扬，像是在为知道什么而得意；而序号为偶数的，人物都闭着眼但眉头紧锁，像是在忍受什么痛苦。
    所有画框下方有黄铜铭牌，但字迹模糊，像是有人特意刮花。
    铭文的刮痕似乎能组成一个单词：<b>DONOTCOUNT</b>
    * * 进一步观察
        -> Act2_3
    * * 好像没什么有用的信息了，到此为止吧
        -> Act2_1
*   观察空画框
*   先不看了
- 
-> END
= Act2_3
你仔细地看了又看。你发现愉悦的壁画人物眼皮有银色反光，痛苦肖像则没有。正中间的愉悦肖像右眼角有一滴泪（画上去的吗？）。
*   触摸肖像
    伸手触摸第一幅肖像。它不是油画，是某种皮革或<b>皮肤</b>。很温暖，像是活体一样。
    你触摸的瞬间，不知道是不是你的错觉，所有的肖像眼睛同时睁开了！它们的眼球全黑，第一幅肖像变成了你的脸！
    你眨了眨眼，它们又变成了原样，似乎，从未有过异样。
*   我知道的已经够多了。
-   -> Act2_1
= Act2_5

-> END
= hidden0
你抬腕看了一眼表：9:30分。时间尚早。一个穿灰色风衣的老人在巷口抽烟。你刚想上前询问，他看了你一眼，摇头离开。背后的青铜门缓缓开启一条缝，只言片语传入你耳中
“又一个？今年第几个了...”
“第七个。前六个都没通过。”
“这个看起来...不一样。他的编号是0427。”
“那个编号？我以为永远不会再用......”
门缝中飘出一张残破的纸，落在你脚边。上面是手写体：
<b>“如果你在阅读这段文字，转身离开。这份工作需要的是无知者，而非探寻者。--前0427号”</b>
正当你思考着这句话的含义，<>
->->
= Choice0
*   删除邮件，当它是垃圾邮件或恶作剧。[]你删除了邮件。
    <b> 有些人选择不翻开第一页。你的故事尚未开始。</b>
    -> END
*   回复邮件，询问详情[]你怀着疑惑的心情，发送了心中的疑问。
    自动回复：“问题过多者不适合。<b>资格取消。</b>”
    -> END
*   准时赴约，但保持警惕
    ->->
*   提前一小时到达，观察环境
    ~ inAd = true
    ->->
=== function came_from(-> node)
    ~ return TURNS_SINCE(node) == 0
